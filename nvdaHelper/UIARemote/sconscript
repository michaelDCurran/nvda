###
# This file is a part of the NVDA project.
# URL: http://www.nvda-project.org/
# Copyright 2021 NV Access Limited.
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License version 2.0, as published by
# the Free Software Foundation.
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# This license can be found at:
# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html
###

import sysconfig
import os

Import(
	[
		"env",
		"localLib",
	]
)


py_version = sysconfig.get_python_version()
py_base = sysconfig.get_config_var("installed_base")
py_inc = os.path.join(py_base, "include")
py_libpath = os.path.join(py_base, "libs")
py_lib = f"python{py_version.replace('.', '')}"

env = env.Clone()
env.Append(CPPPATH=[
	"#include/pybind11/include",
	py_inc,
])
env.Append(LIBPATH=[py_libpath])

UIARemoteLib = env.SharedLibrary(
	target="_lowLevel",
	source=[
		env["projectResFile"],
		"lowLevel.cpp",
	],
	LIBS=[
		"runtimeobject",
		"UIAutomationCore",
		"oleaut32",
		"ole32",
		localLib[2],
		py_lib,
	],
	SHLIBSUFFIX=".pyd",
)

Return('UIARemoteLib')
